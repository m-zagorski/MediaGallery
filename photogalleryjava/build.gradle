buildscript {
    repositories {
        maven { url 'https://maven-central.storage.googleapis.com' }
        mavenCentral()
        jcenter()
        maven { url 'https://commondatastorage.googleapis.com/maven-repository/' }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'com.appunite'
archivesBaseName = 'photogalleryjava'
version = '1.0.0'

ext {
    appDescription = "Media Gallery Java module for android"
    appWebsiteUrl = "https://github.com/m-zagorski/media-gallery"
    appGirUrl = 'https://github.com/m-zagorski/media-gallery.git'
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'com.appunite'
            artifactId 'photogalleryjava'
            version '1.0.0'
        }
    }
}

bintray {
    user = project.ext.has("bintray.user") ? project.ext.get("bintray.user") : ""
    key = project.ext.has("bintray.apikey") ? project.ext.get("bintray.apikey") : ""
    publications = ['MyPublication']
    pkg {
        repo = 'maven'
        name = archivesBaseName
        desc = appDescription
        userOrg = 'mateusz'
        licenses = ['Apache-2.0']
        websiteUrl = appWebsiteUrl
        vcsUrl = appGirUrl
        publish = true
        publicDownloadNumbers = true
        version {
            gpg {
                sign = true
            }
        }
    }
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

configurations {
    provided
    testProvided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
    test {
        compileClasspath += configurations.testProvided
    }
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url 'https://commondatastorage.googleapis.com/maven-repository/' }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.dagger:dagger:2.4'
    provided 'com.google.dagger:dagger-compiler:2.4'
    compile "io.reactivex:rxjava:1.0.14"
    compile 'javax.annotation:jsr250-api:1.0'
    compile 'com.github.jacek-marchwicki.rx-java-extensions:rx-extensions:1.0.4'
    compile 'com.google.auto.value:auto-value:1.2'
    compile 'org.pcollections:pcollections:2.1.2'

    testCompile "org.codehaus.groovy:groovy-all:2.4.1"
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
    testRuntime "cglib:cglib-nodep:3.1"
    testRuntime "org.objenesis:objenesis:2.1"

    testCompile 'com.google.dagger:dagger:2.4'
    testCompile 'com.google.dagger:dagger-compiler:2.4'
    testCompile "com.google.code.findbugs:jsr305:2.0.1"
    testProvided "javax.annotation:javax.annotation-api:1.2"
    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'com.google.truth:truth:0.26'
}

task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/classes/test"
}
processTestResources.dependsOn copyTestResources

test {
    maxParallelForks 10
}